plugins {
    id 'java'
    id 'com.mendix.gradle.publish-module-plugin' version '1.12'
    id 'net.researchgate.release' version '2.8.1'
}

import org.gradle.api.internal.file.copy.CopySpecInternal

apply from: 'environment.gradle'

sourceCompatibility = '11'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext {
    mendixPublicApiVersion = '9.18.3'
    userLibDir = 'userlib'
}

mxMarketplace {
    moduleName    = "AuditTrail"
    moduleLicense = "Apache V2"
    appNumber     = 138
    appName       = "Audittrail"
    appDirectory      = "."
    versionPathPrefix = "__Version " // the path prefix within the module to the version folder
}

repositories {
    maven {
        url 'https://nexus.rnd.mendix.com/repository/repo1-proxy/'
    }
    maven {
        url 'https://nexus.rnd.mendix.com/repository/maven-hosted/'
    }
    mavenLocal {
        mavenContent { snapshotsOnly() }
    }
}


dependencies {
    compileOnly([group: 'com.mendix', name: 'public-api', version: "$mendixPublicApiVersion"])
    testCompileOnly("com.mendix:public-api:$mendixPublicApiVersion")

    testImplementation('com.mendix.util:junit-helper:1.0-SNAPSHOT') {
        exclude group: 'com.mendix', module: 'public-api'
    }

    testImplementation(
        [group: 'junit', name: 'junit', version: '4.13.1'],
        [group: 'org.hamcrest', name: 'hamcrest', version: '2.2']
    )
}

sourceSets {
    main {
        java {
            srcDir 'javasource'
        }
        resources {
            srcDirs = ['resources', 'build/generated/resources']
        }
    }
}

tasks.register('copyComponents', Copy) {
    from file("${rootDir}/deployment/run/component.xml")
    into file("${rootDir}/build/generated/resources/OSGI-INF/.")
}

processResources  {
    dependsOn copyComponents
}

test {
    useJUnit()
    jvmArgs = ["-Djava.io.tmpdir=$temporaryDir"]
    environment 'MX_INSTALL_PATH',mxInstallPath
    classpath += fileTree("${mxInstallPath}/runtime/bundles").include('*.jar')
    doFirst {
        println(mxInstallPath)
    }
}

task copyAllToUserlib(type: Copy) {
    from configurations.testCompileClasspath
    into userLibDir
}

task copyRuntimeToUserlib(type: Copy) {
    from configurations.runtimeClasspath
    into userLibDir
    eachFile { fileCopyDetails -> new File(destinationDir, "${fileCopyDetails.name}.${project.name}.RequiredLib").write '' }
}

clean {
    delete "$userLibDir"
}

task prepareMpk {
    dependsOn 'clean'
    dependsOn 'copyRuntimeToUserlib'

    tasks.findByName('copyRuntimeToUserlib').mustRunAfter 'clean'
}

task prepareTest {
    dependsOn 'clean'
    dependsOn 'copyAllToUserlib'

    tasks.findByName('copyAllToUserlib').mustRunAfter 'clean'
}

release {
    tagTemplate = '$name-$version'
}

task afterReleaseBuildTask {
    dependsOn 'prepareMpk'
    dependsOn 'prepareTest'
    dependsOn 'test'
    dependsOn 'publishModuleToMarketplace'

    tasks.findByName('prepareTest').mustRunAfter 'prepareMpk'
    tasks.findByName('test').mustRunAfter 'prepareTest'
    tasks.findByName('publishModuleToMarketplace').mustRunAfter 'test'
}

exportModule.dependsOn prepareMpk
mxBuild.dependsOn prepareTest
afterReleaseBuild.dependsOn afterReleaseBuildTask