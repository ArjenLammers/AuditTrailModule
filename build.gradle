plugins {
    id 'java'
    id 'com.mendix.gradle.publish-module-plugin' version '1.12'
    id 'net.researchgate.release' version '2.8.1'
}

apply from: 'environment.gradle'

sourceCompatibility = '11'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

project.ext {
    mendixPublicApiVersion = '9.18.3'
    userLibDir = 'userlib'
}

mxMarketplace {
    moduleName    = "AuditTrail"
    moduleLicense = "Apache V2"
    appNumber     = 138
    appName       = "Audittrail"
    appDirectory      = "."
    versionPathPrefix = "__Version " // the path prefix within the module to the version folder
}

repositories {
    maven {
        url 'https://nexus.rnd.mendix.com/repository/repo1-proxy/'
    }
    maven {
        url 'https://nexus.rnd.mendix.com/repository/maven-hosted/'
    }
}

dependencies {
    compileOnly([group: 'com.mendix', name: 'public-api', version: "$mendixPublicApiVersion"])
    testCompileOnly("com.mendix:public-api:$mendixPublicApiVersion")

    testImplementation('com.mendix.util:junit-helper:1.0.0') {
        exclude group: 'com.mendix', module: 'public-api'
    }

    testImplementation(
        [group: 'junit', name: 'junit', version: '4.13.1'],
        [group: 'org.hamcrest', name: 'hamcrest', version: '2.2']
    )
}

sourceSets {
    main {
        java {
            srcDir 'javasource'
        }
        resources {
            srcDirs = ['resources', 'build/generated/resources']
        }
    }
}

tasks.register('copyComponents', Copy) {
    from file('deployment/run/component.xml')
    into 'build/generated/resources/OSGI-INF/'
}

processResources  {
    dependsOn copyComponents
}

test {
    useJUnit()
    jvmArgs = ["-Djava.io.tmpdir=$temporaryDir"]
    environment 'MX_INSTALL_PATH',mxInstallPath
    classpath += fileTree("${mxInstallPath}/runtime/bundles").include('*.jar')
}

clean {
    delete "$userLibDir"
}

release {
    tagTemplate = '$name-$version'
}

tasks.register('copyRuntimeToUserlib', Copy) {
    from configurations.runtimeClasspath
    into userLibDir
    eachFile { fileCopyDetails -> new File(destinationDir, "${fileCopyDetails.name}.${project.name}.RequiredLib").write '' }
    dependsOn 'clean'
}

tasks.register('afterReleaseBuildTask') {
    dependsOn 'copyRuntimeToUserlib'
    dependsOn 'test'
    dependsOn 'publishModuleToMarketplace'
}

exportModule.dependsOn copyRuntimeToUserlib
afterReleaseBuild.dependsOn afterReleaseBuildTask
